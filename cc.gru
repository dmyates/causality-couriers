# '#' means this line (or the rest of this line) is a comment
# (if you want to output '#' during your game, use "&amp;num;")
game Causality Couriers # Game title
id CCLD53 # game ID (used to identify cookies; should be unique on your site)
author David Yates
version 1.0.2
person 2 # 2nd person, i.e. "You are in..."
examine on # if 'on', the verb 'examine' will be implemented by clicking nouns
conversation on
wait off
status { $room.display } | { $room.year } # '|' divides left and right ends of status bar
colour statusbar_background b9b7a7 # https://coolors.co/086375-5f8789-8a9993-b5aa9d-b7b1a2-b9b7a7-000000-0c1821-222d35
colour statusbar_text 000000
colour main_background b5aa9d          
colour main_text 000000                
colour main_prompt 000000              
colour main_text_old 22d35            
colour main_prompt_old 22d35          
colour instructions_button 5F8789    
colour instructions_button_text 000000 
colour room_background b9b7a7           
colour room_text 000000                 
colour room_button 5F8789                
colour room_button_text 000000         
colour conversation_background b7b1a2  
colour conversation_text 000000
colour conversation_button 5F8789
colour conversation_button_text 000000
colour examine_button 5F8789
colour examine_button_text 000000
colour inventory_background b5aa9d
colour inventory_text 000000
colour inventory_button 5F8789
colour inventory_button_text 000000
colour inventory_inactive_text 22d35
colour save_background 0c1821
colour save_button 5F8789
colour save_button_text 000000
colour page_background 0c1821
localise intro_for_instructions_click &amp;nbsp;
localise intro_instructions_button how to play
localise instructions This is a text adventure game built in Robin Johnson's Gruescript. The main action takes place in this pane. In the panes below, you can see where you are, what's there, and what you're carrying. To move somewhere or interact with something, click one of the buttons. You can click on the names of objects to take a closer look at them. Finally, you can click an item in the carrying list to hold that item, which may make more actions available. Good luck!
localise intro_by by
localise you_see_nothing_special_about_that {The $this} is just an ordinary, everyday {$this}. It looks, sounds and smells exactly as you would expect it to.
localise you_can_also_see What's here:
# Introductory text
say You're standing in front of barricade built of stone and rusted steel, a twisted plastic collection tag in your hands. The dizzy nausea you get after teleporting has yet to subside. It's always worse when there's a time shift involved.
say
say Rusty corrugated iron sheets and the twisted remains of cars and lawnmowers fill in the gaps between large stone slabs above you, occasionally creaking ominously. A brown, blasted landscape stretches around you as far as the eye can see.
say 
say You double-check your collection tag. Yep, this must be the place. But how to get in?
say
js hold('collection_tag') # hold tag at start of game

var illustration title # set first illustration
var titlegone 0

rule
eq $titlegone 0
assign titlegone 1
js show_illustration('barricade')

## PART ONE: COLLECTION
thing sword Courier's Blade
desc This is the first job you've had where a massive sword is considered required equipment. Carrying it around on your safer deliveries sometimes makes you feel a bit silly, but it's saved your life { $this.savedlife }. It is, of course, no ordinary sword. The blade is sharp enough to slice through metal as though cutting cheese. Despite its large size, it's very light and easy to wield. 
prop savedlife a couple of times now # this should change after you destroy the drone
carried
tags portable

verb drop sword
!at apartment
say This sword has saved your life enough times to earn a permanent spot on your back.

verb drop sword
at apartment 
say You hang the Courier's Blade in its usual spot above your television.
continue

verb examine sword
at apartment
!carried $this
say The sword is hanging up above your television. People have complimented you on the cool decoration, unaware of its true purpose.

thing collection_tag collection tag
desc &amp;nbsp;
tags portable
carried
cverbs twist

verb examine collection_tag # hack to have a multiline description
tag $this examined
say A small green plastic tag, rectangular in shape, with no obvious special properties. On one side, a translucent sheet overlays a slip of white paper, printed with collection instructions. These read as follows&amp;colon;
say
say <center>Waybill&amp;colon; CC00554273</center>
say <center>Dimensions&amp;colon; 30x8 cm</center>
say <center>Weight&amp;colon; 1.2 kg</center>
say <center>Collection address&amp;colon;</center>
say <center>Darius Fletcher, Giant Barricade, Outer Wastes, Undisclosed</center>
say
say Because collection tags function as teleporters, addresses don't need to be too specific.

verb twist collection_tag
say Twisting a collection tag will instantly transport the holder to the place and time of the parcel's collection. But only once, and you've already used this one.

## In front of the barricade
room before_barricade You're standing in front of an enormous barricade made of junk. { before_barricade.goin }
prop display Before the junk barricade
prop year Unknown year
prop teleport_sentence The junk barricade and the desert blend blend into a colourful blur as you spin through time and space.
prop goin There is no obvious entrance.
dir desert before_barricade
tags start

verb go desert
at before_barricade
say It doesn't look like there's anything else for miles around. Besides, if you wander too far off course, you'll be teleported back home, and you'll get a warning for an incomplete job.

thing barricade
name barricade
desc It looks like it's been here for a long time, slowly, almost organically growing. There's at least half a junkyard's worth of discarded machinery and twisted metal, mixed with half a quarry's worth of stone slabs.
loc before_barricade
tags immovable impenetrable

setverb knock_on barricade
!has barricade knocked

verb knock_on barricade
tag $this knocked
say Feeling like an idiot, you approach the barricade and rap your knuckles against the smooth green-painted surface of the least-rusted fender you can locate. "Hello, anyone in there?" you ask.
say
say There is no response, as you expected.
say
say You look around at the nothing that stretches in every direction. Until you get this parcel, you're stuck here. You knock again, repeatedly and with greater force.
say
say This time, a mechanical buzz greets you. It's coming from somewhere above your head. You look up, but have to shield your eyes from the harsh sun. You can just make out a descending circular shadow.
bring drone

thing drone circular shadow
name circular shadow
desc It's a flying mechanical helicopter drone.
prop callme drone
tags alive hostile
verbs wave_at

verb examine drone
!has $this examined
run x_drone

proc x_drone
!has $this examined
run introduce drone
tag $this examined
say On closer inspection, it appears to be a flying mechanical helicopter drone.

verb wave_at drone
run x_drone
!has $this waved: It knows you're here already!
tag $this waved
say You wave at the drone. There's loud clicking sound, audible over the whir of its rotors, and bright red beam shines directly in your eyes. You cry out and look away. The drone clicks again, and its rotors get louder. It's coming straight for you!
js show_illustration('drone')

var dodge_count 0

setverb dodge drone
has $this waved

verb dodge drone
add dodge_count 1
random dodge_text 5
!lt $dodge_count 10: { dodge_variants.$dodge_text } # first five dodges are successful
!eq $dodge_count 11: { dodge_variants_tiring.$dodge_text } # still successful, but tiring
chance 1 2: { dodge_variants_tiring.$dodge_text } # still successful, but tiring
say You try to duck out of the drone's way, but it swoops down to meet you. Before you can move again, it's upon you. You scream in agony as the blades slice your face open.
say
say Next time, consider doing something other than endlessly dodging the homicidal drone.
say
run the_end 1

thing dodge_variants
prop 1 You dive out of the drone's path just as it lunges for you. Its rotor blades miss your ear by inches. Undeterred, the drone turns around and fixes its red light on you in preparation for another strike.
prop 2 You sidestep the drone's attack, narrowly avoiding its whirring blades. But it quickly turns around, ready to make another attempt. Its menacing red light glares at you.
prop 3 As the drone swoops towards you, you duck out of the way just in time, feeling the rush of wind from its blades. Undaunted, the drone pivots around, its unblinking red eye tracking your every move.
prop 4 With a quick step to the left, you manage to evade the drone's attack, feeling the rush of air as it whizzes past you. But the drone is relentless, and it hovers menacingly in front of you, its red light fixated on your position.
prop 5 With a quick step to the right, you manage to evade the drone's attack, feeling the rush of air as it whizzes past you. But the drone is relentless, and it hovers menacingly in front of you, its red light fixated on your position.

thing dodge_variants_tiring
prop 1 You dive out of the drone's path just as it lunges for you. Its rotor blades miss your ear by less than an inch. Undeterred, the drone turns around and fixes its red light on you in preparation for another strike. Blood pounds against your ears.
prop 2 You sidestep the drone's attack, but its whirring blades nick the sleeve of your uniform. It quickly turns around, ready to make another attempt. Its menacing red light glares at you.
prop 3 As the drone swoops towards you, you duck out of the way just in time, feeling the rush of wind from its blades. Undaunted, the drone pivots around, its unblinking red eye tracking your every move. Your breathing is heavy, and you're not sure how much longer you can keep this up.
prop 4 With a step to the left, you manage to evade the drone's attack. The rush of air as it whizzes past nearly knocks you off balance. The drone is relentless, and it hovers menacingly in front of you, its red light fixated on your position.
prop 5 With a lurch to the right, you manage to evade the drone's attack, feeling the rush of air as it whizzes past you. That was too close. But the drone is relentless, and it hovers menacingly in front of you, its red light fixated on your position.

setverb strike drone
carried sword
gt $dodge_count 0

verb strike drone
run draw_sword
say Eyes fixed on the drone's beady red light, you lower yourself into a fighting stance, gripping the hilt of your sword firmly with both hands. The drone clicks and comes for you, but you're ready.
say
say With a mighty swing, you bring the massive blade down, just as the drone comes within striking distance. There's a horrible grinding sound, and the motion of the drone's rotors sends vibrations down your arms. But you hold fast. Metal slides against metal as you force the sword down.
say
say Two halves of the drone drop at your feet, smoking. The red light has gone out for good.
hide drone
bring destroyed_drone
write sword.savedlife three times now
js show_illustration('droneexplode')

thing destroyed_drone
desc It's been sliced clean in two. From either side, tendrils of black smoke curl up into the sky.

var turns_since_drone_destroyed 0

rule # wait after drone destruction
here destroyed_drone
add turns_since_drone_destroyed 1

rule # notice drone destruction
eq before_barricade.dir.opening 0
eq $turns_since_drone_destroyed 2
write before_barricade.goin A dark opening has appeared near the middle.
say
say You're busy contemplating the remains of the thing that tried to kill you when you notice movement on the barricade. A stone slab is being hauled up, accompanied by the sound of squeaky pulleys and loud metallic scraping, to reveal a dark opening.
open before_barricade opening wooden_shed
js show_illustration('barricadeopen')

verb go opening
at before_barricade
!has wooden_shed visited
js show_illustration('shed')
say One hand on the hilt of your sword, you step cautiously into the opening. As your foot crosses the threshold, an electric light blinks on. It's dim and inconstant, but provides enough light for you to see you're in a tight hallway. The ceiling is quite low, forcing you to stoop, and the walls so close that you have no room to draw your sword.
say
say "Come in, come in!" says a voice from the end of the hall, which appears to open out into a larger room. "Sorry about the drone! Thought you were someone else."
say
say For the sake of the job, you proceed through the hallway.
say
goto wooden_shed
tag wooden_shed visited

verb go opening
at before_barricade
has wooden_shed visited
js show_illustration('shed')
continue

## Wooden shed
room wooden_shed You find yourself in a dimly lit wooden shed. The walls are lined with photographs, newspaper clippings and drawing pins attached by strings. { wooden_shed.fletcher }
prop display Wooden shed
prop year Unknown year
prop fletcher A shifty character stands in front of a desk, regarding you curiously.
prop teleport_sentence The conspiracy-laden walls of Fletcher's small room blend into a gray blur as you spin through time and space.
dir outside before_barricade
#tags start # for testing

verb go outside
at wooden_shed
js show_illustration('barricadeopen')
continue

thing walls
desc The wood surface of the wall is barely visible beneath the hundreds of photographs, newspaper clippings, printed emails and memoranda covering it. Many are complete with captions scrawled in red ink, and all are connected with different coloured strings. All the standard conspiracy theories are covered – you can see stuff about the moon landing, JFK, flat earth, hollow earth, chemtrails, ancient aliens, lizard people, Freemasons, the Knights Templar, the Bilderburg Group, Nessie and Bigfoot. There's a bold circle around a photograph of the Great Pyramid of Giza.
loc wooden_shed

verb examine walls
tag $this examined
continue

thing fletcher shifty character
name shifty character
desc { $this.subject} looks to be in his sixties, with a white beard and a mane of grey hair. He's dressed in filthy jeans and a shirt the same colour as the blasted ground outside. His small green eyes fix on you from their sunken sockets, below brows creased with concern. It has been years since he last smiled.
tags alive male conversation
loc wooden_shed
prop callme Fletcher
prop subject The man
prop start_conversation "You must be the courier," says the man, looking you up and down. "Name's Fletcher." Your collection tag mentions a Darius Fletcher – this must be him.
prop end_conversation "G'bye then."

verb talk fletcher
js show_illustration('fletcher')
continue

verb talk fletcher
!has fletcher introduced
run introduce $this
assign $this.subject $this.callme
write wooden_shed.fletcher A shifty character named Fletcher stands in front of a desk, regarding you curiously.
continue

verb end_conversation fletcher # change intro line following introduction
!has fletcher introduced
write $this.start_conversation "Yeah?" asks Fletcher, shifting the toothpick in his mouth.
tag fletcher introduced
continue

verb end_conversation fletcher # change intro line following introduction
js show_illustration('shed')
continue

setverb ask_fletcher year
!has fletcher asked_about_year

verb ask_fletcher year
tag fletcher asked_about_year
say "What year is it?" Fletcher raises an eyebrow. "It's thirty years' too late, that's what year it is. Hell, these days I'm given to thinkin' it's a few millennia too late."
say
say Your facial expression must have betrayed a dissatisfaction with this answer, so Fletcher continues, "I was told you wouldn't ask too many questions. Just do this job for me, and it won't matter what year this is anymore."

setverb ask_fletcher payment
!has fletcher asked_about_payment

verb ask_fletcher payment
tag fletcher asked_about_payment
say "You know, I admire the initiative, but I wasn't born yesterday. I've already made the payment to your employer in full."

setverb ask_fletcher drone
!has fletcher asked_about_drone

verb ask_fletcher drone
tag fletcher asked_about_drone
say "Yeah, sorry again," Fletcher says, not looking all that sorry. "Our security system can be a little overzealous. But honestly, if you were able to take it out that easily, we probably need to upgrade anyway. I told Stice we needed the ones with AKs!"

setverb ask_fletcher barricade
!has fletcher asked_about_barricade

verb ask_fletcher barricade
tag fletcher asked_about_barricade
say Fletcher raises an eyebrow. "Best fortifications this side of the Crater! But I guess you're not from around here. Obviously. They murdered the last Causality Couriers in '47."
say 
say Fletcher chews his toothpick for a moment. "When did you say you're from?" You tell him. "Back then, eh? Well, maybe take an early retirement, would be my advice."

setverb ask_fletcher walls
has walls examined
!has fletcher asked_about_walls

verb ask_fletcher walls
tag fletcher asked_about_walls
say "Oh, that?" Fletcher glances around at the dense array of photographs and newspaper clippings and pantomimes laughter. "You might say I'm a student of history. I've been trying to figure out just where it was that things went so far wrong..."

setverb ask_fletcher parcel
!has fletcher asked_about_parcel

verb ask_fletcher parcel
carried collection_tag: Fletcher asks to see the collection tag. You pat your pockets frantically but can't find it. You apologise and ask him to wait one moment. He frowns.
say You show Fletcher your collection tag. He squints at it for a moment, and then nods. "I've got the parcel right here," he says, walking around to the other side of the desk. He fumbles around with the drawers for a moment, and finally pulls out a tall, thin box wrapped in brown paper. A green delivery tag hangs off one side.
say
say You take the box from him and hand him the collection tag as a receipt.
give tall_parcel
hide collection_tag
tag fletcher asked_about_parcel

### Parcel
thing tall_parcel tall parcel
name tall parcel
desc A tall, thin rectangular parcel marked FRAGILE.
tags portable parcel dtagged
prop shake_count 0
prop triedopening 0
prop tamperworry &amp;nbsp;

verb drop tall_parcel
say You're not letting this parcel out of your site until it's safely delivered. Causality Couriers gives out very heavy fines to couriers who lose their parcels.

setverb open tall_parcel
!has $this open

setverb shake tall_parcel
!has $this open

verb shake tall_parcel
add $this.shake_count 1
say You hear a sloshing sound through the wrapping.

verb open tall_parcel
!here fletcher: You really shouldn't tamper with parcels entrusted to your care. Especially not right in front of a customer.
!here alien: You really shouldn't tamper with parcels entrusted to your care. Especially not right in front of a customer.
add $this.triedopening 1
gt $this.triedopening 1: As a Causality Courier, you really shouldn't tamper with parcels entrusted to you... even if it is tempting...
gt $this.triedopening 2: As curious as you are about the contents of this package, you really shouldn't open it...
gt $this.triedopening 3: The temptation is too great, you're not sure how much longer you can stop yourself from opening this package...
gt $this.triedopening 4: Are you really sure you want to do this?
tag $this open
tag $this tampered
write $this.tamperworry You hope the alien won't notice that you've tampered with it.
give wine_bottle
write $this.desc A tall, thin rectangular parcel marked FRAGILE. It's been illegally opened.
say Against your better judgement, you open the parcel. There's a wine bottle inside.

setverb close tall_parcel
has $this open

verb close tall_parcel
carried wine_bottle: There's no point in closing it if it's empty. Make sure you're carrying something to put inside – preferably the original contents.
run close_parcel

proc close_parcel
has wine_bottle sealed
hide wine_bottle
untag $this open
write $this.desc A tall, thin rectangular parcel marked FRAGILE. You know what you did.
say You put the wine bottle back in the parcel and close it back up. 

proc close_parcel
!has wine_bottle sealed
hide wine_bottle
untag $this open
write $this.desc A tall, thin rectangular parcel marked FRAGILE. You know what you did.
say Luckily for you, this bottle of wine has a screw-on cap that you're able to replace. After replacing the cap, you put the wine bottle back in the parcel and close it back up. 

setverb remove_tag tall_parcel
held $this
has $this dtagged

verb remove_tag tall_parcel
say You pull the tag off the tall parcel. Its waybill number, CC00554273, matches the one on your collection tag.
untag $this dtagged
unhold
give delivery_tag
js hold('delivery_tag')

thing delivery_tag delivery tag
name delivery tag
desc &amp;nbsp;
tags portable
cverbs twist

verb examine delivery_tag # hack to have a multiline description
say A small green plastic tag, rectangular in shape, with no obvious special properties. On one side, a translucent sheet overlays a slip of white paper, printed with delivery instructions. These read as follows&amp;colon;
say
say <center>Waybill&amp;colon; CC00554273</center>
say <center>Dimensions&amp;colon; 30x8 cm</center>
say <center>Weight&amp;colon; 1.2 kg</center>
say <center>Delivery address&amp;colon;</center>
say <center>Gthxrynn of the Ythrgzlyrs
say <center>Pyramid Construction Site, Giza, Kingdom of Egypt, 2500 BC</center>
say
say Below the English instructions, a series of pictograms depict half a pyramid, and a stick figure offering a tall, thin box up to a many-pointed black star.
!has $this examined
tag $this examined
say
say You've seen some strange tags in this business, but this one takes the cake.

setverb ask_fletcher delivery
has delivery_tag examined

verb ask_fletcher delivery
say You ask Fletcher if the delivery tag is for real.
say
say He looks offended. "Twist it and see, pal."

verb twist delivery_tag
!has $this twisted: Twisting a delivery tag will instantly transport the holder to the place and time where the parcel is to be delivered. But only once, and you've already used this one.
assign conversation 0
tag $this twisted
say Grasping the tag firmly in both hands, you give it a hard twist. {$room.teleport_sentence}
say
say Once the spinning blur in front of your eyes has turned predominantly yellow, it slows down and resolves itself into distinct objects. Clear blue sky, bright burning sun, rolling yellow sands. The sounds of yelling – an unfamiliar language – and of stone scraping against stone. The smell of all-pervading dust, and the feeling of dry heat.
say 
say You've travelled to another desert, one from long ago. Right in time to watch the construction of a monument that will last into your own time – the Great Pyramid of Giza, King Khufu's final resting place. But it's what you see when you turn around that really shocks you.
say
goto egypt
js show_illustration('pyramid')

# Parcel contents
thing wine_bottle wine bottle
tags portable poisoned sealed
desc A smooth glass bottle containing red wine. The colour is deep and rich. There is no label or other indication of what kind of wine it is. The bottle { $openstatus }.
prop triedopening 0
prop trieddrinking 0
prop triedpouring 0
prop openstatus is unopened

verb drop wine_bottle
!has wine_bottle empty
say Dropping this bottle would cause it to shatter, sending wine and shards of glass everywhere. Let's save the clean-up and not do that.

verb drop wine_bottle
has wine_bottle empty
say Dropping this bottle would cause it to shatter, sending shards of glass everywhere. Let's save the clean-up and not do that.

setverb open wine_bottle
carried $this
has $this sealed

verb open wine_bottle
!here fletcher: It's probably not a good idea to show Fletcher you've been tampering with his parcel.
!here alien: You can't flash that around in front of its intended recipient!
add $this.triedopening 1
gt $this.triedopening 1: You shouldn't open the wine bottle, but then, you shouldn't have opened the parcel either. There's still time to turn back...
gt $this.triedopening 2: You really want to open this, eh?
untag $this sealed
write $this.openstatus has been opened
say You open the bottle and take a whiff. It smells like the perfect crime.

setverb drink wine_bottle
carried $this
!has $this sealed
!has $this empty

verb drink wine_bottle
!here fletcher: Fletcher probably wouldn't appreciate watching you take a swig of his wine.
!here alien: At least have enough discretion not to drink the wine in front of its intended recipient!
add $this.trieddrinking 1
gt $this.trieddrinking 1: Well, you've opened it now, but that doesn't mean you have to drink it. Though you are feeling quite thirsty...
has $this poisoned
say You lift the bottle to your lips and take a swig, without even waiting for the tannins to settle. The wine has a full-bodied flavour, and you note hints of vanilla and– and– a terrible, overpowering bitterness!
say
say You clutch at your stomach and double over, overcome with dizziness. The inside of your mouth is on fire, burning like you've never felt before. The feeling spreads from your mouth, down your throat, and into your chest.
say
say The last thing you hear is shattering glass, as the wine bottle drops from your hand.
say
run the_end 2

setverb empty wine_bottle
carried $this
!has $this sealed
!has $this empty

verb empty wine_bottle
!here fletcher: Fletcher probably wouldn't appreciate you pouring out his wine right in front of him.
!here alien: You can't just empty the bottle in front of its intended recipient.
add $this.triedemptying 1
gt $this.triedemptying 1: It would be a bit of a waste to empty it, not to mention the vandalism. But if you really want to do it, you can...
tag $this empty
untag $this poisoned
write $this.desc A smooth, unlabelled glass bottle that once contained red wine.
say You tip the bottle over and watch the rich red liquid pours out onto the sand, first a torrent, then a trickle. Finally, you shake the bottle to wring out the last few drops. At your feet, a damp patch spreads. It smells faintly of almonds.

## PART TWO: DELIVERY

# Egypt
room egypt You're standing in front of a large work site that will one day be the Great Pyramid of Giza. { egypt.aliendesc } You can also see a tent.
prop display Great Pyramid Construction Site, Giza
prop aliendesc There appears to be an alien here, watching the construction.
prop year ~2500 BC
dir desert egypt_desert
dir tentflap inside_tent
#tags start # for testing

thing tent tent
name tent
desc The tent is small and dome-shaped. It appears to made from goat skin.
loc egypt
verbs enter

verb enter tent
!has alien_remains reinforced: Alien portals block your path!
!here alien_remains: You don't need to get alien blood all over someone else's tent.
goto inside_tent
js show_illustration('tent')

verb go tentflap
at egypt
has alien_remains reinforced
say Alien portals block your path!

verb go tentflap
at egypt
here alien_remains
say You don't need to get alien blood all over someone else's tent.

verb go tentflap
js show_illustration('tent')
continue

verb go outside
at inside_tent
js show_illustration('pyramid')
continue

room inside_tent You've slipped into a small goat-skin tent near the pyramid construction site. There's a fire pit in the middle and a camel-hair blanket against the far side. Whoever owns this tent is not here right now.
prop display Inside a tent
prop year ~2500 BC
dir outside egypt

thing fire_pit fire pit
name fire pit
desc It's not lit right now, but you can see charred logs from the last time it was.
loc inside_tent

thing camel_blanket camel-hair blanket
name camel-hair blanket
desc A scratchy-looking blanket made out of camel-hair, presumably used by the tent's occupant for sleeping under.
loc inside_tent
tags portable

verb take camel_blanket
say It doesn't look very comfortable, and its owner probably needs it more than you do.

verb go desert
at egypt
carried tall_parcel
say You've got to deliver this parcel before you can go wandering off.
#give tall_parcel # for testing

thing alien Tall Alien
name Tall Alien
desc An enormous black star-shaped creature with a leathery hide and a mass of tentacles. Looking directly at it may eventually drive you mad.
loc egypt
tags alive male conversation
prop callme Gthxrynn
prop start_conversation Feeling slightly foolish, you greet the alien in 21st century English.<br><br>To your great surprise, the alien replies immediately. "Hello, my name is Gthxrynn," it says. "I'm not from around here. It doesn't look like you are either." Its voice is slightly tinny, as if produced by a speaker, but it's entirely fluent, with a warm, familiar tone that contrasts greatly with its frightening appearance.
prop end_conversation "Farewell, courier. May your travels be safe and prosperous."

verb talk alien
run introduce $this
write egypt.aliendesc Gthxrynn of the Ythrgzlyrs stands on a small dune, watching the construction.
js show_illustration('alien')
continue

verb end_conversation alien # change intro line
write $this.start_conversation "Hello again, courier," says Gthxrynn.
js show_illustration('pyramid')
continue

setverb ask_alien payment
has alien disappointed
!has alien asked_about_payment

verb ask_alien payment
tag alien asked_about_payment
say "You want payment for the delivery of a parcel I wasn't expecting? Earth customs certainly are bizarre. Oh well, here you go."
say
say The squelching sounds that follow make you regret your question. After some time, Gthxrynn extends a tentacle and deposits three writhing grey slugs in your outstretched hands. "I think I got the currency conversion right," says the alien.
give slugs

thing slugs slugs
name slugs
desc Apparently the Ythrgzlyrs use live slugs as money. They squirm around in your palms, leaving a sticky residue that makes you thoroughly regret trying to hustle Gthxrynn.
tags portable

setverb ask_alien language
!has alien asked_about_language

verb ask_alien language
tag alien asked_about_language
say "You ask how I can understand you? Oh, human languages are all pretty similar, past and future. Certainly nothing the translator implanted in my... brain, I suppose you'd call it... can't handle." The alien waggles a few of its tentacles. "We Ythrgzlyrs pride ourselves on linguistics, you know."

setverb ask_alien pyramid
!has alien asked_about_pyramid

verb ask_alien pyramid
tag alien asked_about_pyramid
say "I have come from very far away to watch the construction of this famous pyramid and learn what I may from its builders. We Ythrgzlyrs are masters of space, but we've never quite been able to figure out architecture, you know."

setverb ask_alien learnings
has alien asked_about_pyramid

verb ask_alien learnings
chance 1 2: "Oh, what have I learnt so far? Well, it's ingenious how they've rigged up all these ramps and levers to move such big blocks of stone around. But I suppose one has to invent such things to compensate for the deficiencies of such feeble, squat bodies. No offence."
say "Oh, what have I learnt so far? Well, they appear to have highly advanced knowledge of mathematics and especially geometry. Amazing that they can fit all that knowledge in such small heads. No offence."

setverb ask_alien name
!has alien asked_about_name

verb ask_alien name
tag alien asked_about_name
say "It's pronounced Gthxrynn. Gthxrzzkynn without the zzk, if that helps."

setverb tell_alien delivery
#try has_any_parcel
carried tall_parcel
carried delivery_tag
!has alien told_about_delivery

verb tell_alien delivery
tag alien told_about_delivery
say "A delivery? For me? Are you sure?"
say
say You show {alien.name} the delivery tag, holding it up to where you think its eye-equivalents might be.
say
say After a moment, the alien speaks again. "Well, so it is. What a nice surprise!"
js hold('tall_parcel')

setverb deliver_to_alien tall_parcel
carried tall_parcel
here alien
has alien told_about_delivery

verb deliver_to_alien tall_parcel
carried delivery_tag: You'll need the delivery tag as well.
!has $this open: You can't just deliver an opened parcel! At least try to make it look presentable first...
assign conversation 0
hide $this
hide delivery_tag
say You hold {the $this} out to { alien.name }, along with its delivery tag. { $this.tamperworry }
say
run alien_drink

# If the bottle is empty
proc alien_drink
has wine_bottle empty
say { alien.name } thanks you graciously as it wraps a tentacle around the tall parcel. There's a quick swish, and suddenly the parcel is open, and another of the alien's tentacles is holding an empty glass bottle. 
say
say The alien's features contort in what is probably a gesture of confusion. "Why would anyone send me this?"
say
say You shrug.
say
say "It's a pity too, from the smell of the bottle, I'm sure I would have greatly enjoyed this wine. Oh well, probably just someone playing a joke on old {alien.name}! Thank you for the delivery, courier!"
tag alien disappointed
assign ending 2
js show_illustration('alien')

setverb ask_alien bottle
has alien disappointed

verb ask_alien bottle
say "A few of my friends know I quite enjoy Earth wines. Maybe I did something to upset one of them..."

verb go desert
at egypt
here alien
has alien disappointed
say With your job done, you trudge off into the desert, walking in the direction of nothing in particular. Better not to suddenly teleport in front of a bunch of ancient Egyptian slaves.
say
say You haven't gone very far before you feel a familiar tingling sensation and the world starts to spin.
say
run teleport_home

# If the bottle is full
proc alien_drink
!has wine_bottle empty
has wine_bottle poisoned
say { alien.name } thanks you graciously as it wraps a tentacle around the tall parcel. There's a quick swish, and suddenly the parcel is open, and another of the alien's tentacles is holding an unmarked bottle of red wine. 
say
say "Marvellous! My favourite vintage."
say
say With astonishing dexterity, {alien.name} uses a single tentacle to pop the top off the bottle and then aim it at the hole you take to be its mouth-equivalent. There's a sharp sucking sound, and the bottle is drained instantly.
say
say Then the alien explodes.
hide alien
write egypt.aliendesc Bits of smoking alien flesh lie scattered across the sand.
write tent.desc The tent is small and dome-shaped. It appears to made from goat skin. Like everything else, it's covered in alien remains.
bring alien_remains
js show_illustration('aliendie')

thing alien_remains alien remains
name alien remains
desc Two explosions in one day. You're surrounded (and covered!) by leathery bits of {alien.name}. Maybe wine doesn't agree with Ythrgzlyrs.

var turns_since_alien_died 0

rule # wait after alien death
here alien_remains
add turns_since_alien_died 1

rule # notice alien death
!has alien_remains reinforced
eq $turns_since_alien_died 2
tag alien_remains reinforced
say
say There's a sudden popping sound to the east. And the west. And the north. And every other direction, all at once. Everywhere you look, swirling black mists are appearing, with creatures that looks a lot like Gthxrynn emerging from them.
say
say And even though you're still not sure where their eyes are, you get the distinct impression that they're all bearing down on you. It's fight or flight.
bring alien_swarm
js show_illustration('portals')

thing alien_swarm alien swarm
name alien swarm
desc An army of nightmarish black tentacle aliens surrounds you, with more appearing every moment. 

# try to fight aliens
setverb fight_aliens sword
carried sword
here alien_remains
has alien_remains reinforced

setverb strike alien_swarm
carried sword
here alien_remains
has alien_remains reinforced

verb fight_aliens sword
run alien_attack

verb strike alien_swarm
run alien_attack

proc alien_attack
say Brandishing your sword, you fly at the nearest portal, which an alien is poking its head out of. An unholy scream rends the air as you bring your sword down on its neck, and a spray of bright green blood splatters across your face.
say
say As you turn to face the next alien, you realise that this may not have been a good idea. Three aliens have emerged from their portals, and are advancing on you, with another four close on their tails. Additional portals continue to materialise all around you.
say
say You hold your ground valiantly, swinging your sword and slicing a commendable amount of alien flesh. But the aliens keep advancing. Before long, you feel crushing grip of a massive tentacle against your windpipe.
say
say The sword slips from your grasp as you gasp for air, and the world goes dark.
say
run the_end 3

# wait too long and die through inaction
rule
has alien_remains reinforced
eq $turns_since_alien_died 5
say
say So many portals have appeared that the sky is entirely obscured by black squirming aliens. You're surrounded on all sides. The aliens are so close that you can smell their putrid odour.
say
say A tentacle brushes against your arm, followed by another, and another. You try to reach for your sword, but merely grasp at another tentacle. You can feel a crushing pressure across your chest as the world turns black.
run the_end 4

# Escape aliens
verb go desert
at egypt
here alien_remains
!has alien_remains reinforced
say You're still in shock. 

verb go desert
at egypt
here alien_remains
has alien_remains reinforced
here alien_swarm
say You're not going to wait around to see what these aliens want. You look around and spy a gap between two portals that have just materialised. As long as you can get far enough away, your Causality Courier homeward teleportation will kick in, and you'll be able to put a few millennia between yourself and the Ythrgzlyrs.
say
say Head down, you sprint for the gap. Surprised yelps crackle from the speakers of the Ythrgzlyrs' translation devices, but most are still busy climbing out of their portals. Before long, you feel a familiar tingling sensation and the world starts to spin.
say
run teleport_prison

# PART THREE: CONCLUSION
#var ending 1 # for testing
#var ending 2 # for testing

room apartment You're in your apartment, a welcome sight after a harrowing jaunt through time and space. {apartment.additional}
prop display Your apartment
prop year Present day
prop additional It's good to be home.
#tags start # for testing

thing television TV
name TV
desc Your high-definition smart TV, purchased with the proceeds from your last delivery.
loc apartment

## Prison ending
proc teleport_prison
assign ending 1
goto apartment
write apartment.additional Something feels a little different from usual.
say Your surroundings resolve into the familiar colours of your apartment, and you immediately crash on the couch.
say
js show_illustration('apartment')

setverb turn_on television
!has television turnedon

verb turn_on television
tag television turnedon
tag television soap
say You grab the remote from its resting place next to you on the couch and turn on the television. A day-time soap opera is playing.
eq $ending 1 # prison ending
say
say A sharp pain flashes through your temple. It's a familiar headache, one you often get after finishing a job. It's got something to do with the changes your delivery has made to the past.

setverb watch television
has television turnedon
!has television sickof

verb watch television
has television soap
say Two celebrated character actors are declaring their undying love to each other, as one lies in a hospital bed.
eq $ending 1 # prison ending
say
say You head is still throbbing. You've never had it this badly before.
tag television sickof

setverb turn_off television
has television sickof

verb turn_off television
say Your skull feels like it's going to split open, and the noise and light from the television isn't helping. But as you're reaching for the remote to turn it off, the picture suddenly changes.
say
say Gone is the day-time soap opera, replaced with a slick newsroom. A male anchor is sitting next to a Ythrgzlyr anchor. You remember liking the Ythrgzlyr anchor, Fthryzzk. Such benevolent overlords, those Ythrgzlyrs.
say
say Wait, that can't be right. You'd never even heard of the Ythrgzlyrs before today. What's going on?
untag television sickof
untag television soap
tag television emergency
var emergency_counter 0

rule
here television
has television emergency
say
say {emergency_broadcast.$emergency_counter}
add emergency_counter 1

rule
here television
has television emergency
eq $emergency_counter 3
write apartment.additional The state-mandated wanted poster of Public Enemy &amp;num;1 hangs on the wall opposite your front door.
bring wanted_poster

rule
here television
has television emergency
eq $emergency_counter 4
open apartment outside prison

verb go outside
at apartment
say Looks like it's time to face the music. Wearily, you lift yourself off the couch and open your front door to the police amassed outside, stretching your arms as high as they'll go.
say
run arrest

rule
here television
has television emergency
eq $emergency_counter 6
say
say "HANDS UP!"
say
say You see no choice but to comply.
say
assign $emergency_counter 999
run arrest

thing emergency_broadcast
prop 0 "We interrupt your regularly scheduled programming to bring you an emergency broadcast," says the human anchor on the television. Strangely, he sounds positively giddy with excitement.
prop 1 "This is a momentous day for the human race," continues Fthryzzk, the alien anchor, speaking through a tinny translator. "A project millennia in the making is at last coming to fruition. Ryan, why don't you do the honours?"<br><br>You've never changed things this much before...
prop 2 "Thank you Fthryzzk," says the human anchor, Ryan. "We are just now receiving reports of an impending arrest. The arrest of Public Enemy Number One."<br><br>You know who Public Enemy Number One is. The memory has just come into your head, but it's always been there. It helps that Earth's Ythrgzlyr overlords require every human subject to keep a poster depicting Public Enemy Number One in their homes.<br><br>You hadn't noticed yours before, but at the same time, it's always been there.
prop 3 "We're cutting to a live feed now," says Fthryzzk. The anchors disappear and are replaced with a grainy view of your apartment's front door. Human and alien police officers crowd around it.
prop 4 The sounds on the television match the sounds you can hear from behind your door. A gruff voice shouts, "Open up, we've got you surrounded!" Perhaps you have no choice.
prop 5 After a minute of waiting, a couple of officers step up to your front door. "Looks like they're going to have to bust it open, Fthryzzk," says the disembodied voice of Ryan the human news anchor.
prop 6 On the television screen, you see the police officer take a step back and then ram his shoulder into your front door. Your attention is instantly diverted as, at the same time, your front door bursts open and five or six police officers burst into your living room, all pointing guns at you.

thing wanted_poster wanted poster
name poster of Public Enemy &amp;num;1
desc It's a poster of you. You're dressed in your grey Causality Courier uniform, splotched with bright green alien blood. There's a murderous expression on your face. The reward below your face is not an amount in any currency, but the single word FREEDOM.

proc arrest
say You are arrested for the murder of Gthxrynn of the Ythrgzlyrs. As you learn, but have also always known, humanity has been under the yoke of the Ythrgzlyrs for over four thousand years, as collective penance for your actions. The Ythrgzlyrs don't understand or really believe in time travel, but are otherwise inconceivably powerful, so this was how they decided to bring you to justice.
say
say Life under alien occupation is not all bad. The Ythrgzlyrs aren't petty or authoritarian, and in many ways they act as friends to humanity, sharing their wisdom and technology. The new memories trickling into your mind seem idyllic in comparison to the old ones you're slowly forgetting.
say
say As Public Enemy Number One, you're not exactly popular, so you get a cell to yourself, in an isolated wing of the prison, where you must now await trial. You tried to explain the circumstances of the Gthxrynn's death, that you were merely a messenger, but your Ythrgzlyr lawyer, Xzthlghrrt, says it's basically unprovable. And you have no way of contacting the Causality Couriers.
say
say With all the changes you've made to the past, do they even exist anymore?
say
goto prison
bring wanted_poster
all carried
hide $this
js show_illustration('prison')

room prison You're in a prison cell, awaiting trial.
prop display Your cell
prop year Present day

thing harmonica harmonica
name silver harmonica
desc A small silver harmonica, perfect for playing the prison blues.
loc prison
tags portable
verbs play

verb examine wanted_poster
at prison
tag wanted_poster examined
say This poster is a deeply familiar image. You've seen it in every house you've ever been to. But until your arrest, you never recognised your own face on it. Seemingly no-one else did either.
say
say In another sense, you had never seen it until after you returned from your fateful delivery. Every memory is superimposed on another. The strongest ones are full of absurdities such as this. For the passage of causality is not a clockwork mechanism, perfectly calibrated, but a writhing snake that imposes itself through force.

verb play harmonica
!carried harmonica
give harmonica
continue

verb play harmonica
tag harmonica played
say You sit down on your prison bed and play your prison harmonica. Its sorrowful sound fills the room.

rule
at prison
has harmonica played
has wanted_poster examined
!has collection_tag_two appeared
say
say You hear a pop, followed by a sizzle. A tiny bolt of lightning flashes before your eyes, and a piece of green plastic falls to the floor.
bring collection_tag_two
tag collection_tag_two appeared

thing collection_tag_two collection tag
name collection tag
desc The instructions on this collection tag are written in an unfamiliar script. Looking at it makes you feel as through spiders are crawling across your eyeballs.
tags portable
cverbs twist

verb examine collection_tag_two
!carried $this
say A collection tag lies on the floor of your cell. Looks like the company hasn't forgotten about you after all.

verb twist collection_tag_two
say You twist the collection tag. The room starts to spin.
say
run the_end 5

## Lecture ending
proc teleport_home
assign ending 2
say Your surroundings resolve into the familiar colours of your apartment, and you immediately crash on the couch.
write apartment.additional Your living room's main features are a couch, television and coffee table. There's a sealed letter in the middle of the coffee table.
goto apartment
bring envelope
js show_illustration('apartment')

thing envelope sealed envelope
name sealed envelope
desc A crisp, cream-coloured envelope. It's addressed to you and bears the logo of the Causality Couriers.
tags portable sealed

setverb open envelope
has $this sealed

verb open envelope
give $this
untag $this sealed
say You tear open the envelope and pull out a letter. Or rather, a neatly folded blank sheet of paper. The envelope is otherwise empty.
say
say As you're puzzling over this, a humanoid figure made of pure light appears in your living room. The figure hovers over your coffee table, legs in a lotus position.
bring humanoid_figure

thing humanoid_figure being of pure light
name being of pure light
desc A human figure made of yellow light. The figure has no identifying details – it is more the idea of a person than anyone specific. It's painful to look at the figure directly, and you find yourself shielding your eyes.
tags conversation alive justarrived
prop start_conversation "Hello." The figure speaks in a rich, deep voice.
prop end_conversation "Goodbye."

verb talk humanoid_figure
js show_illustration('lightbeing')
continue

setverb ask_humanoid_figure appearance
has humanoid_figure justarrived

verb ask_humanoid_figure appearance
untag humanoid_figure justarrived
tag humanoid_figure knows
say "I am not a real person, merely a message. I represent the management of Causality Couriers. We are aware of the deviations from standard procedure that occurred during your latest job."
say
say You feel your heart sink. Should you try to justify your actions, or just apologise?

setverb tell_humanoid_figure apologise
has humanoid_figure knows

setverb tell_humanoid_figure justify
has humanoid_figure knows

verb tell_humanoid_figure apologise
untag humanoid_figure knows
tag humanoid_figure integrity
say "No apology is required, or expected. Causality Couriers has received payment for this delivery, and the terms of the contract have been fulfilled. As per clause 8.7A, couriers can exercise their discretion over the effects of a package on the timestream, provided a delivery is ultimately made."
say
say While you're mostly relieved to hear this, you make a mental note never to send a package of your own with Causality Couriers.

verb tell_humanoid_figure justify
untag humanoid_figure knows
tag humanoid_figure integrity
say "No justification is required, or expected. Causality Couriers has received payment for this delivery, and the terms of the contract have been fulfilled. As per clause 8.7A, couriers can exercise their discretion over the effects of a package on the timestream, provided a delivery is actually made."
say
say While you're mostly relieved to hear this, you make a mental note never to send a package of your own with Causality Couriers.
say
say "However, a deduction of 100 credits will be made from your pay for this job."
say
say You wince. The job was only worth 125.

setverb tell_humanoid_figure integrity
has humanoid_figure integrity

verb tell_humanoid_figure integrity
untag humanoid_figure integrity
tag humanoid_figure onyourown
say "Causality Couriers has no official position on the integrity of the timeline. Individual couriers are expected to use their own judgement. Management will only intervene in a delivery should there be reasonable cause to believe that said delivery may prevent the founding of Causality Couriers, or the receipt of funds due to Causality Couriers." 

verb end_conversation humanoid_figure
has $this onyourown
assign conversation 0
say
say "Your payment will be processed in two to three business days. Goodbye." 
say
say The light being disappears, leaving you to contemplate the awesome responsibility of your delivery job. You glance at the coffee table and notice that the being has left behind a green plastic collection tag.
say
run the_end 6
js show_illustration('apartment')

# General procedures
proc has_any_parcel # succeeds if any item with the parcel tag is carried
count inventory_weight carried
!eq $inventory_weight 0
sequence carried # fails open if nothing carried, so line above needed
has $this parcel

proc introduce individual # make intro & change descriptions to person's name
assign $individual.display $individual.callme
assign $individual.name $individual.callme
continue

proc draw_sword
!held sword
say You reach back and draw the Courier's Blade from its scabbard.
js hold('sword') # dirty hack because there's no way to force holding

# Game endings
proc the_end the_ending
say <center><b>THE END</b>
say
run postmortem $the_ending
js document.getElementById('illustration-img').style.filter = 'grayscale(100%)';

proc postmortem the_ending
eq $the_ending 1
say You got the <b>Killed by a Drone Ending</b> (1/6)
say
say Delivery status&amp;colon; PARCEL NOT COLLECTED
say Payment received&amp;colon; 0 credits
say
die

proc postmortem the_ending
eq $the_ending 2
say You got the <b>Poisoned Ending</b> (2/6)
say
say Delivery status&amp;colon; LOST IN TRANSIT
say Payment received&amp;colon; -1000 credits (to be paid by next of kin)
say Times parcel was shaken&amp;colon; {tall_parcel.shake_count}
run payment_enquiries
say
die

proc postmortem the_ending
eq $the_ending 3
say You got the <b>Go Down Fighting Ending</b> (3/6)
say
say Delivery status&amp;colon; COMPLETE
say Payment received&amp;colon; 125 credits (to next of kin)
say Times parcel was shaken&amp;colon; {tall_parcel.shake_count}
run payment_enquiries
say
die

proc postmortem the_ending
eq $the_ending 4
say You got the <b>Hesitation Kills Ending</b> (4/6)
say
say Delivery status&amp;colon; COMPLETE
say Payment received&amp;colon; 125 credits (to next of kin)
say Times parcel was shaken&amp;colon; {tall_parcel.shake_count}
run payment_enquiries
say
die

proc postmortem the_ending
eq $the_ending 5
say You got the <b>Radically Altered History Ending</b> (5/6)
say
say Delivery status&amp;colon; COMPLETE
say Payment received&amp;colon; 125 credits
say Times parcel was shaken&amp;colon; {tall_parcel.shake_count}
run payment_enquiries
say
die

proc postmortem the_ending
eq $the_ending 6
say You got the <b>Maverick Ending</b> (6/6)
say
say Delivery status&amp;colon; DAMAGED IN TRANSIT
say Payment received&amp;colon; 25 credits
say Times parcel was shaken&amp;colon; {tall_parcel.shake_count}
run payment_enquiries
say
die

proc payment_enquiries
has fletcher asked_about_payment
has alien asked_about_payment
say You've earned the <b>Hustler Achievement</b>, for attempting to extract an extra payment from both the consigner and consignee.
